plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
}

android {

    signingConfigs {
        key {
            storeFile file('key.pfx')
            storePassword 'demokey'
            keyAlias 'demo'
            keyPassword 'demokey'
        }
    }

    compileSdkVersion 31

    defaultConfig {
        applicationId "eu.pcosta.ethereumwallet"
        minSdkVersion 28
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        // Keys
        Properties properties = new Properties()
        properties.load(project.rootProject.file('keys.properties').newDataInputStream())
        buildConfigField "String", "ETH_ACCOUNT", "${properties.getProperty('ETH_ACCOUNT')}"
        buildConfigField "String", "ETHERSCAN_API_KEY", "${properties.getProperty('ETHERSCAN_API_KEY')}"
        buildConfigField "String", "ETHPLORER_API_KEY", "${properties.getProperty('ETHPLORER_API_KEY')}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.key
        }
        release {
            signingConfig signingConfigs.key
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures.viewBinding true
}

dependencies {
    // Android
    implementation "androidx.core:core-ktx:1.7.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.4.1"
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:2.4.1"

    // Design
    implementation "androidx.appcompat:appcompat:1.4.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.3"
    implementation "com.google.android.material:material:1.5.0"
    implementation 'com.airbnb.android:lottie:5.0.3'

    // Navigation
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.1'

    // RxJava + Kotlin and Android extensions
    implementation "io.reactivex.rxjava3:rxkotlin:3.0.1"
    implementation "io.reactivex.rxjava3:rxandroid:3.0.0"

    // Retrofit
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava3:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"

    // Moshi JSON
    def moshi_version = "1.13.0"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    implementation "com.squareup.moshi:moshi-adapters:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"

    // Koin
    implementation "io.insert-koin:koin-android:3.1.5"

    // Room
    def room_version = "2.4.2"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava3:$room_version"

    // RxBindings
    implementation 'com.jakewharton.rxbinding4:rxbinding-appcompat:4.0.0'

    // Unit Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.mockito.kotlin:mockito-kotlin:4.0.0"
    //noinspection GradleDependency. Kluent removes mockito support above 1.64
    testImplementation 'org.amshove.kluent:kluent-android:1.64'

    // UI Testing
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
    androidTestImplementation('com.adevinta.android:barista:4.2.0') {
        exclude group: 'org.jetbrains.kotlin'
    }
}